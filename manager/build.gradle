apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "moe.shizuku.privileged.api"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "versionName", "${rootProject.ext.versionName}"

        externalNativeBuild {
            cmake {
                arguments '-DANDROID_STL=c++_static'
            }
        }
        ndk {
            abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java', project(':shared').file('src/main/java')]
        }
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path 'src/main/jni/CMakeLists.txt'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "shizuku-manager-${variant.versionCode}.apk"

            if (variant.getBuildType().isMinifyEnabled()) {
                variant.assemble.doLast {
                    copy {
                        from variant.mappingFile
                        into "${rootDir}/${project.name}/release"
                        rename { String fileName ->
                            "mapping-${variant.versionCode}.txt"
                        }
                    }
                    copy {
                        from variant.outputs*.outputFile
                        into "${rootDir}/${project.name}/release"
                    }
                }
            }
        }
    }
}

android.applicationVariants.all { variant ->
    String taskName = String.format("remove%sStubClasses", variant.name.capitalize())
    String compileJavaTaskName = String.format("compile%sJavaWithJavac", variant.name.capitalize())
    task "${taskName}" {
        doLast {
            delete "${buildDir}/intermediates/javac/${variant.name}/${compileJavaTaskName}/classes/android"
        }
    }
    variant.processJavaResources.dependsOn(taskName)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation "androidx.core:core:1.0.2"
    implementation "androidx.fragment:fragment:$androidXLibraryVersion"
    implementation "androidx.recyclerview:recyclerview:$androidXLibraryVersion"
    implementation "androidx.lifecycle:lifecycle-extensions:2.0.0"
    implementation 'com.github.topjohnwu.libsu:core:2.3.3'
    implementation "moe.shizuku.preference:preference:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-dialog-android:$preferenceLibraryVersion"
    implementation "moe.shizuku.preference:preference-simplemenu:$preferenceLibraryVersion"
    implementation "moe.shizuku.support:design:3.1.2"
    implementation 'moe.shizuku.support:support-utils:3.0.6'
    implementation 'moe.shizuku.support:recyclerview-utils:3.0.4'
    implementation "moe.shizuku.support:htmlcompat:2.0.0"
    implementation 'moe.shizuku.fontprovider:api:10'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.7'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'moe.shizuku.privilege:api-base:12'
    implementation 'moe.shizuku.privilege:api:3.0.0-alpha8'
    implementation 'me.weishu:free_reflection:2.0.0' // bypass hidden api restriction
}

apply from: rootProject.file('signing.gradle')

preBuild.dependsOn ':server:dex'