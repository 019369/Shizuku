apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "moe.shizuku.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

// task to remove all .class generated from stub class, or app will crash if proguarded
// if you use other plugin, you must find where the plugin generate file and delete them
android.applicationVariants.all { variant ->
    String taskName = String.format("remove%sStubClasses", variant.name.capitalize())
    String compileJavaTaskName = String.format("compile%sJavaWithJavac", variant.name.capitalize())
    task "${taskName}" {
        doLast {
            delete "${buildDir}/intermediates/javac/${variant.name}/${compileJavaTaskName}/classes/android"
        }
    }
    variant.processJavaResourcesProvider.get().dependsOn(taskName)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    implementation "androidx.localbroadcastmanager:localbroadcastmanager:1.0.0"
    implementation "androidx.annotation:annotation:1.1.0"
    implementation "androidx.fragment:fragment:1.2.0"
    implementation project(':api') // shizuku v3

    implementation 'me.weishu:free_reflection:2.2.0' // bypass hidden api restriction
}

apply from: rootProject.file('signing.gradle')