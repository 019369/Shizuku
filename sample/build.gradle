apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        applicationId "moe.shizuku.sample"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName rootProject.ext.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        sign
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.sign
        }
        release {
            signingConfig signingConfigs.sign
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

android.applicationVariants.all { variant ->
    String taskName = String.format("remove%sStubClasses", variant.name.capitalize())
    String compileJavaTaskName = String.format("compile%sJavaWithJavac", variant.name.capitalize())
    task "${taskName}" {
        doLast {
            delete "${buildDir}/intermediates/javac/${variant.name}/${compileJavaTaskName}/classes/android"
        }
    }
    variant.processJavaResources.dependsOn(taskName)
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
    implementation "androidx.annotation:annotation:$androidXLibraryVersion"
    implementation "androidx.fragment:fragment:$androidXLibraryVersion"
    implementation project(':api') // shizuku v3

    implementation 'me.weishu:free_reflection:1.2.0' // bypass hidden api restriction
}

apply from: rootProject.file('signing.gradle')