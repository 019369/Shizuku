apply plugin: 'com.android.library'

android {
    compileSdkVersion rootProject.ext.targetSdkVersion
    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.versionCode
        versionName '3.0.0-alpha10'
        manifestPlaceholders = [versionName: versionName]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            consumerProguardFiles 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            java.srcDirs = ['src/main/java', project(':api-shared').file('src/main/java'), project(':server-api').file('src/main/java')]
        }
    }
}

android.libraryVariants.all { variant ->
    String taskName = String.format("remove%sStubClasses", variant.name.capitalize())
    task "${taskName}" {
        doLast {
            delete "${buildDir}/intermediates/javac/${variant.name}/compile${variant.name}JavaWithJavac/classes/android"
        }
    }
    variant.processJavaResourcesProvider.get().dependsOn(taskName)
}

ext {
    libraryName = 'Shizuku V3'
    libraryDescription = 'Shizuku V3'
    libraryVersion = android.defaultConfig.versionName
    artifact = "api"
    bintrayName = "api"
}

apply from: project.rootProject.file('bintray.gradle')

dependencies {
    implementation "androidx.annotation:annotation:$androidXLibraryVersion"
    //compile project(':api-shared')
}